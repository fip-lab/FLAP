////////////////////////////////////////////////////////////////////
// A simple binary version of the cell transition model (CTM) for 
// modeling traffic.  Based on the original CTM Tech Report and its 
// specification as a factored MDP in the following papers:
//
//   The Cell Transition Model: Network Traffic.  Daganzo; 
//   Tech Report Berkeley Institute of Transport Studies, 1994.
//
//   Efficient Solutions to Factored MDPs with Imprecise Transition 
//   Probabilities.  Delgado, Sanner, de Barros, Cozman; ICAPS, 2009.
//
// Because of the binary variable and no intermediate variable
// restrictions for the IPPC 2011, this model is quite simplified
// and ignores traffic aspects such as turns and turn probabilities.
//
// Note that this model uses concurrent actions, but that the number
// of total actions will only ever be 2^(# intersections).  Refer to
// the IPPC email list if you are unsure how to handle concurrent
// actions.
//
// Author: Scott Sanner (ssanner [at] gmail.com)
////////////////////////////////////////////////////////////////////
domain traffic_mdp {
  
	requirements = { 
		reward-deterministic, // this domain does not use a stochastic reward
		constrained-state,    // this domain uses state constraints
		concurrent            // this domain permits multiple non-default actions
	};

	types {
  		cell : object;
		intersection : object;
	};
 	
	pvariables { 

		// Specify which cells are perimeter input cells and their input rates
		PERIMETER-INPUT-CELL(cell) :  { non-fluent, bool, default = false };
		PERIMETER-INPUT-RATE(cell) :  { non-fluent, real, default = 1.0 };

		// Specify which cells are exit cells
		PERIMETER-EXIT-CELL(cell) :   { non-fluent, bool, default = false };

		// Specify which cells flow into other cells
		FLOWS-INTO-CELL(cell, cell) : { non-fluent, bool, default = false }; 

		// Specify which cells can pass into intersection on a signal phase
		FLOWS-INTO-INTERSECTION-NS(cell, intersection) : { non-fluent, bool, default = false }; 
		FLOWS-INTO-INTERSECTION-EW(cell, intersection) : { non-fluent, bool, default = false }; 

		// This is a simple boolean encoding of signal state for an intersection
		//
		// light-signal 1 2 -> effective light state
		// =========================================
		//              0 0 -> all red
		//              0 1 -> green for north-south traffic flow 
		//              1 1 -> all red
		//              1 0 -> green for east-west traffic flow
		light-signal1(intersection) : { state-fluent, bool, default = false };
		light-signal2(intersection) : { state-fluent, bool, default = false };

		// Binary cell transition model (CTM): cell is either occupied or not
		occupied(cell) : { state-fluent, bool, default = false };

		// Do we advance this signal for an intersection to its next sequence?
		advance(intersection) : { action-fluent, bool, default = false }; 
	};
  
	cpfs {
  
  		// Just use a finite state machine for the light-signals
  		// Note: a light signal that is red *must* advance to the next state...
  		//       there would be no reason to hold a red signal indefinitely. 
  		light-signal1'(?i) = 
  			if (advance(?i) | (light-signal1(?i) ^ light-signal2(?i)) | (~light-signal1(?i) ^ ~light-signal2(?i)))
 			then // Advance to next state (see table above)
  				KronDelta( light-signal2(?i) )
  			else // No change 
  				KronDelta( light-signal1(?i) );
 
  		light-signal2'(?i) = 
  			if (advance(?i) | (light-signal1(?i) ^ light-signal2(?i)) | (~light-signal1(?i) ^ ~light-signal2(?i)))
  			then // Advance to next state (see table above)
  				KronDelta( ~light-signal1(?i) )
  			else // No change 
  				KronDelta( light-signal2(?i) );
    
  		// Update a cell's occupation status according to CTM rules
  		occupied'(?c) = // Check for perimeter cell
  						if (PERIMETER-INPUT-CELL(?c))
  						then [if (~occupied(?c))
  								then Bernoulli(	PERIMETER-INPUT-RATE(?c) ) // Empty
  							else if (exists_{?c2 : cell} [FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2)])
  								then KronDelta(	false ) // Vacated  
  							else KronDelta( true )] // Stopped
  							
  						// Check for cell entering intersection on green light
  						else if ([exists_{?i : intersection} [light-signal2(?i) ^ ~light-signal1(?i) ^ FLOWS-INTO-INTERSECTION-NS(?c,?i) ^ exists_{?c2 : cell} [FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2)]]]
  		   						 | [exists_{?i : intersection} [light-signal1(?i) ^ ~light-signal2(?i) ^ FLOWS-INTO-INTERSECTION-EW(?c,?i) ^ exists_{?c2 : cell} [FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2)]]])
						then [if (~occupied(?c))
  							then KronDelta( exists_{?c2 : cell} [FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2)] )
  							else KronDelta( false )] // Vacated since cell enters intersection
								
						// Check for occupied cell entering intersection (if get here, must be red)
  						else if (exists_{?i : intersection} ((FLOWS-INTO-INTERSECTION-NS(?c,?i) | FLOWS-INTO-INTERSECTION-EW(?c,?i)) ^ occupied(?c))) 
  		   					then KronDelta( true ) // car stuck at red light
								
						// Check cells ?c that take traffic exiting an intersection
						else if ( exists_{?i : intersection, ?c2 : cell} (FLOWS-INTO-INTERSECTION-NS(?c2, ?i) | FLOWS-INTO-INTERSECTION-EW(?c2, ?i)) ^ FLOWS-INTO-CELL(?c2, ?c) )
						then [if (occupied(?c))
							// Can car go forward?
							then KronDelta( ~exists_{?c2 : cell} FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2) ) 
							// Did a car enter from intersection?
							else KronDelta( 
								[exists_{?i : intersection} [light-signal2(?i) ^ ~light-signal1(?i) ^ exists_{?c2 : cell} [FLOWS-INTO-INTERSECTION-NS(?c2,?i) ^ FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2)]]]
  		   						 | [exists_{?i : intersection} [light-signal1(?i) ^ ~light-signal2(?i) ^ exists_{?c2 : cell} [FLOWS-INTO-INTERSECTION-EW(?c2,?i) ^ FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2)]]]
  		   					)] 
								
						// Must be a normal cell - normal transition rules apply
						else if (occupied(?c)) // Does it empty?
						then KronDelta ( ~PERIMETER-EXIT-CELL(?c) ^ ~exists_{?c2 : cell} [FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2)])
						else // Does it fill?
  							KronDelta ( exists_{?c2 : cell} [FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2)] );
  
	};

	// Minimize congestion: this reward penalizes congested traffic defined as pairs 
	// of *consecutive* occupied cells
	// Minimize congestion: this reward penalizes congested traffic defined as pairs 
	// of *consecutive* occupied cells
	reward = sum_{?c : cell} -[occupied(?c) ^ exists_{?c2 : cell} (FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2))];
	
	action-preconditions{
		(sum_{?i : intersection} [ advance(?i) ]) <= 4;
	};

}


instance traffic_inst_mdp__08 {
	domain = traffic_mdp;
	objects {
		intersection : {ia6a6,ia6a12,ia12a6,ia12a12};
		cell : {ca6a1,ca1a6,ca6a2,ca2a6,ca6a3,ca3a6,ca6a4,ca4a6,ca6a5,ca5a6,ca6a7,ca7a6,ca6a8,ca8a6,ca6a9,ca9a6,ca6a10,ca10a6,ca6a11,ca11a6,ca6a13,ca13a6,ca6a14,ca14a6,ca6a15,ca15a6,ca6a16,ca16a6,ca6a17,ca17a6,ca12a1,ca1a12,ca12a2,ca2a12,ca12a3,ca3a12,ca12a4,ca4a12,ca12a5,ca5a12,ca12a7,ca7a12,ca12a8,ca8a12,ca12a9,ca9a12,ca12a10,ca10a12,ca12a11,ca11a12,ca12a13,ca13a12,ca12a14,ca14a12,ca12a15,ca15a12,ca12a16,ca16a12,ca12a17,ca17a12};
	};
	non-fluents {

		PERIMETER-INPUT-CELL(ca6a1);
		PERIMETER-INPUT-CELL(ca12a1);
		PERIMETER-INPUT-CELL(ca1a6);
		PERIMETER-INPUT-CELL(ca1a12);

		PERIMETER-INPUT-RATE(ca6a1) = 0.36422506;
		PERIMETER-INPUT-RATE(ca12a1) = 0.2814253;
		PERIMETER-INPUT-RATE(ca1a6) = 0.107539035;
		PERIMETER-INPUT-RATE(ca1a12) = 0.39031354;

		PERIMETER-EXIT-CELL(ca6a17);
		PERIMETER-EXIT-CELL(ca12a17);
		PERIMETER-EXIT-CELL(ca17a6);
		PERIMETER-EXIT-CELL(ca17a12);

		FLOWS-INTO-INTERSECTION-EW(ca6a5,ia6a6);
		FLOWS-INTO-INTERSECTION-EW(ca6a11,ia6a12);
		FLOWS-INTO-INTERSECTION-EW(ca12a5,ia12a6);
		FLOWS-INTO-INTERSECTION-EW(ca12a11,ia12a12);
		FLOWS-INTO-INTERSECTION-NS(ca5a6,ia6a6);
		FLOWS-INTO-INTERSECTION-NS(ca11a6,ia12a6);
		FLOWS-INTO-INTERSECTION-NS(ca5a12,ia6a12);
		FLOWS-INTO-INTERSECTION-NS(ca11a12,ia12a12);

		FLOWS-INTO-CELL(ca6a1,ca6a2);
		FLOWS-INTO-CELL(ca1a6,ca2a6);
		FLOWS-INTO-CELL(ca6a2,ca6a3);
		FLOWS-INTO-CELL(ca2a6,ca3a6);
		FLOWS-INTO-CELL(ca6a3,ca6a4);
		FLOWS-INTO-CELL(ca3a6,ca4a6);
		FLOWS-INTO-CELL(ca6a4,ca6a5);
		FLOWS-INTO-CELL(ca4a6,ca5a6);
		FLOWS-INTO-CELL(ca6a5,ca6a7);
		FLOWS-INTO-CELL(ca5a6,ca7a6);
		FLOWS-INTO-CELL(ca6a7,ca6a8);
		FLOWS-INTO-CELL(ca7a6,ca8a6);
		FLOWS-INTO-CELL(ca6a8,ca6a9);
		FLOWS-INTO-CELL(ca8a6,ca9a6);
		FLOWS-INTO-CELL(ca6a9,ca6a10);
		FLOWS-INTO-CELL(ca9a6,ca10a6);
		FLOWS-INTO-CELL(ca6a10,ca6a11);
		FLOWS-INTO-CELL(ca10a6,ca11a6);
		FLOWS-INTO-CELL(ca6a11,ca6a13);
		FLOWS-INTO-CELL(ca11a6,ca13a6);
		FLOWS-INTO-CELL(ca6a13,ca6a14);
		FLOWS-INTO-CELL(ca13a6,ca14a6);
		FLOWS-INTO-CELL(ca6a14,ca6a15);
		FLOWS-INTO-CELL(ca14a6,ca15a6);
		FLOWS-INTO-CELL(ca6a15,ca6a16);
		FLOWS-INTO-CELL(ca15a6,ca16a6);
		FLOWS-INTO-CELL(ca6a16,ca6a17);
		FLOWS-INTO-CELL(ca16a6,ca17a6);
		FLOWS-INTO-CELL(ca12a1,ca12a2);
		FLOWS-INTO-CELL(ca1a12,ca2a12);
		FLOWS-INTO-CELL(ca12a2,ca12a3);
		FLOWS-INTO-CELL(ca2a12,ca3a12);
		FLOWS-INTO-CELL(ca12a3,ca12a4);
		FLOWS-INTO-CELL(ca3a12,ca4a12);
		FLOWS-INTO-CELL(ca12a4,ca12a5);
		FLOWS-INTO-CELL(ca4a12,ca5a12);
		FLOWS-INTO-CELL(ca12a5,ca12a7);
		FLOWS-INTO-CELL(ca5a12,ca7a12);
		FLOWS-INTO-CELL(ca12a7,ca12a8);
		FLOWS-INTO-CELL(ca7a12,ca8a12);
		FLOWS-INTO-CELL(ca12a8,ca12a9);
		FLOWS-INTO-CELL(ca8a12,ca9a12);
		FLOWS-INTO-CELL(ca12a9,ca12a10);
		FLOWS-INTO-CELL(ca9a12,ca10a12);
		FLOWS-INTO-CELL(ca12a10,ca12a11);
		FLOWS-INTO-CELL(ca10a12,ca11a12);
		FLOWS-INTO-CELL(ca12a11,ca12a13);
		FLOWS-INTO-CELL(ca11a12,ca13a12);
		FLOWS-INTO-CELL(ca12a13,ca12a14);
		FLOWS-INTO-CELL(ca13a12,ca14a12);
		FLOWS-INTO-CELL(ca12a14,ca12a15);
		FLOWS-INTO-CELL(ca14a12,ca15a12);
		FLOWS-INTO-CELL(ca12a15,ca12a16);
		FLOWS-INTO-CELL(ca15a12,ca16a12);
		FLOWS-INTO-CELL(ca12a16,ca12a17);
		FLOWS-INTO-CELL(ca16a12,ca17a12);
	};
	init-state {
		occupied(ca6a1);
		occupied(ca6a5);
		occupied(ca6a8);
		occupied(ca6a11);
		occupied(ca6a14);
		occupied(ca15a6);
		occupied(ca6a16);
		occupied(ca16a6);
		occupied(ca6a17);
		occupied(ca12a3);
		occupied(ca7a12);
		occupied(ca12a10);
		occupied(ca11a12);
		occupied(ca12a14);
		occupied(ca12a16);
	};
	horizon  = 40;
	discount = 1.0;
}


