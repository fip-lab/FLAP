////////////////////////////////////////////////////////////////////
// A simple binary version of the cell transition model (CTM) for 
// modeling traffic.  Based on the original CTM Tech Report and its 
// specification as a factored MDP in the following papers:
//
//   The Cell Transition Model: Network Traffic.  Daganzo; 
//   Tech Report Berkeley Institute of Transport Studies, 1994.
//
//   Efficient Solutions to Factored MDPs with Imprecise Transition 
//   Probabilities.  Delgado, Sanner, de Barros, Cozman; ICAPS, 2009.
//
// Because of the binary variable and no intermediate variable
// restrictions for the IPPC 2011, this model is quite simplified
// and ignores traffic aspects such as turns and turn probabilities.
//
// Note that this model uses concurrent actions, but that the number
// of total actions will only ever be 2^(# intersections).  Refer to
// the IPPC email list if you are unsure how to handle concurrent
// actions.
//
// Author: Scott Sanner (ssanner [at] gmail.com)
////////////////////////////////////////////////////////////////////
domain traffic_mdp {
  
	requirements = { 
		reward-deterministic, // this domain does not use a stochastic reward
		constrained-state,    // this domain uses state constraints
		concurrent            // this domain permits multiple non-default actions
	};

	types {
  		cell : object;
		intersection : object;
	};
 	
	pvariables { 

		// Specify which cells are perimeter input cells and their input rates
		PERIMETER-INPUT-CELL(cell) :  { non-fluent, bool, default = false };
		PERIMETER-INPUT-RATE(cell) :  { non-fluent, real, default = 1.0 };

		// Specify which cells are exit cells
		PERIMETER-EXIT-CELL(cell) :   { non-fluent, bool, default = false };

		// Specify which cells flow into other cells
		FLOWS-INTO-CELL(cell, cell) : { non-fluent, bool, default = false }; 

		// Specify which cells can pass into intersection on a signal phase
		FLOWS-INTO-INTERSECTION-NS(cell, intersection) : { non-fluent, bool, default = false }; 
		FLOWS-INTO-INTERSECTION-EW(cell, intersection) : { non-fluent, bool, default = false }; 

		// This is a simple boolean encoding of signal state for an intersection
		//
		// light-signal 1 2 -> effective light state
		// =========================================
		//              0 0 -> all red
		//              0 1 -> green for north-south traffic flow 
		//              1 1 -> all red
		//              1 0 -> green for east-west traffic flow
		light-signal1(intersection) : { state-fluent, bool, default = false };
		light-signal2(intersection) : { state-fluent, bool, default = false };

		// Binary cell transition model (CTM): cell is either occupied or not
		occupied(cell) : { state-fluent, bool, default = false };

		// Do we advance this signal for an intersection to its next sequence?
		advance(intersection) : { action-fluent, bool, default = false }; 
	};
  
	cpfs {
  
  		// Just use a finite state machine for the light-signals
  		// Note: a light signal that is red *must* advance to the next state...
  		//       there would be no reason to hold a red signal indefinitely. 
  		light-signal1'(?i) = 
  			if (advance(?i) | (light-signal1(?i) ^ light-signal2(?i)) | (~light-signal1(?i) ^ ~light-signal2(?i)))
 			then // Advance to next state (see table above)
  				KronDelta( light-signal2(?i) )
  			else // No change 
  				KronDelta( light-signal1(?i) );
 
  		light-signal2'(?i) = 
  			if (advance(?i) | (light-signal1(?i) ^ light-signal2(?i)) | (~light-signal1(?i) ^ ~light-signal2(?i)))
  			then // Advance to next state (see table above)
  				KronDelta( ~light-signal1(?i) )
  			else // No change 
  				KronDelta( light-signal2(?i) );
    
  		// Update a cell's occupation status according to CTM rules
  		occupied'(?c) = // Check for perimeter cell
  						if (PERIMETER-INPUT-CELL(?c))
  						then [if (~occupied(?c))
  								then Bernoulli(	PERIMETER-INPUT-RATE(?c) ) // Empty
  							else if (exists_{?c2 : cell} [FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2)])
  								then KronDelta(	false ) // Vacated  
  							else KronDelta( true )] // Stopped
  							
  						// Check for cell entering intersection on green light
  						else if ([exists_{?i : intersection} [light-signal2(?i) ^ ~light-signal1(?i) ^ FLOWS-INTO-INTERSECTION-NS(?c,?i) ^ exists_{?c2 : cell} [FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2)]]]
  		   						 | [exists_{?i : intersection} [light-signal1(?i) ^ ~light-signal2(?i) ^ FLOWS-INTO-INTERSECTION-EW(?c,?i) ^ exists_{?c2 : cell} [FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2)]]])
						then [if (~occupied(?c))
  							then KronDelta( exists_{?c2 : cell} [FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2)] )
  							else KronDelta( false )] // Vacated since cell enters intersection
								
						// Check for occupied cell entering intersection (if get here, must be red)
  						else if (exists_{?i : intersection} ((FLOWS-INTO-INTERSECTION-NS(?c,?i) | FLOWS-INTO-INTERSECTION-EW(?c,?i)) ^ occupied(?c))) 
  		   					then KronDelta( true ) // car stuck at red light
								
						// Check cells ?c that take traffic exiting an intersection
						else if ( exists_{?i : intersection, ?c2 : cell} (FLOWS-INTO-INTERSECTION-NS(?c2, ?i) | FLOWS-INTO-INTERSECTION-EW(?c2, ?i)) ^ FLOWS-INTO-CELL(?c2, ?c) )
						then [if (occupied(?c))
							// Can car go forward?
							then KronDelta( ~exists_{?c2 : cell} FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2) ) 
							// Did a car enter from intersection?
							else KronDelta( 
								[exists_{?i : intersection} [light-signal2(?i) ^ ~light-signal1(?i) ^ exists_{?c2 : cell} [FLOWS-INTO-INTERSECTION-NS(?c2,?i) ^ FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2)]]]
  		   						 | [exists_{?i : intersection} [light-signal1(?i) ^ ~light-signal2(?i) ^ exists_{?c2 : cell} [FLOWS-INTO-INTERSECTION-EW(?c2,?i) ^ FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2)]]]
  		   					)] 
								
						// Must be a normal cell - normal transition rules apply
						else if (occupied(?c)) // Does it empty?
						then KronDelta ( ~PERIMETER-EXIT-CELL(?c) ^ ~exists_{?c2 : cell} [FLOWS-INTO-CELL(?c, ?c2) ^ ~occupied(?c2)])
						else // Does it fill?
  							KronDelta ( exists_{?c2 : cell} [FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2)] );
  
	};

	// Minimize congestion: this reward penalizes congested traffic defined as pairs 
	// of *consecutive* occupied cells
	// Minimize congestion: this reward penalizes congested traffic defined as pairs 
	// of *consecutive* occupied cells
	reward = sum_{?c : cell} -[occupied(?c) ^ exists_{?c2 : cell} (FLOWS-INTO-CELL(?c2, ?c) ^ occupied(?c2))];
	
	action-preconditions{
		(sum_{?i : intersection} [ advance(?i) ]) <= 4;
	};

}


instance traffic_inst_mdp__10 {
	domain = traffic_mdp;
	objects {
		intersection : {ia7a7,ia7a14,ia14a7,ia14a14};
		cell : {ca7a1,ca1a7,ca7a2,ca2a7,ca7a3,ca3a7,ca7a4,ca4a7,ca7a5,ca5a7,ca7a6,ca6a7,ca7a8,ca8a7,ca7a9,ca9a7,ca7a10,ca10a7,ca7a11,ca11a7,ca7a12,ca12a7,ca7a13,ca13a7,ca7a15,ca15a7,ca7a16,ca16a7,ca7a17,ca17a7,ca7a18,ca18a7,ca7a19,ca19a7,ca7a20,ca20a7,ca14a1,ca1a14,ca14a2,ca2a14,ca14a3,ca3a14,ca14a4,ca4a14,ca14a5,ca5a14,ca14a6,ca6a14,ca14a8,ca8a14,ca14a9,ca9a14,ca14a10,ca10a14,ca14a11,ca11a14,ca14a12,ca12a14,ca14a13,ca13a14,ca14a15,ca15a14,ca14a16,ca16a14,ca14a17,ca17a14,ca14a18,ca18a14,ca14a19,ca19a14,ca14a20,ca20a14};
	};
	non-fluents {

		PERIMETER-INPUT-CELL(ca7a1);
		PERIMETER-INPUT-CELL(ca14a1);
		PERIMETER-INPUT-CELL(ca1a7);
		PERIMETER-INPUT-CELL(ca1a14);

		PERIMETER-INPUT-RATE(ca7a1) = 0.18904275;
		PERIMETER-INPUT-RATE(ca14a1) = 0.2034848;
		PERIMETER-INPUT-RATE(ca1a7) = 0.26932672;
		PERIMETER-INPUT-RATE(ca1a14) = 0.48115665;

		PERIMETER-EXIT-CELL(ca7a20);
		PERIMETER-EXIT-CELL(ca14a20);
		PERIMETER-EXIT-CELL(ca20a7);
		PERIMETER-EXIT-CELL(ca20a14);

		FLOWS-INTO-INTERSECTION-EW(ca7a6,ia7a7);
		FLOWS-INTO-INTERSECTION-EW(ca7a13,ia7a14);
		FLOWS-INTO-INTERSECTION-EW(ca14a6,ia14a7);
		FLOWS-INTO-INTERSECTION-EW(ca14a13,ia14a14);
		FLOWS-INTO-INTERSECTION-NS(ca6a7,ia7a7);
		FLOWS-INTO-INTERSECTION-NS(ca13a7,ia14a7);
		FLOWS-INTO-INTERSECTION-NS(ca6a14,ia7a14);
		FLOWS-INTO-INTERSECTION-NS(ca13a14,ia14a14);

		FLOWS-INTO-CELL(ca7a1,ca7a2);
		FLOWS-INTO-CELL(ca1a7,ca2a7);
		FLOWS-INTO-CELL(ca7a2,ca7a3);
		FLOWS-INTO-CELL(ca2a7,ca3a7);
		FLOWS-INTO-CELL(ca7a3,ca7a4);
		FLOWS-INTO-CELL(ca3a7,ca4a7);
		FLOWS-INTO-CELL(ca7a4,ca7a5);
		FLOWS-INTO-CELL(ca4a7,ca5a7);
		FLOWS-INTO-CELL(ca7a5,ca7a6);
		FLOWS-INTO-CELL(ca5a7,ca6a7);
		FLOWS-INTO-CELL(ca7a6,ca7a8);
		FLOWS-INTO-CELL(ca6a7,ca8a7);
		FLOWS-INTO-CELL(ca7a8,ca7a9);
		FLOWS-INTO-CELL(ca8a7,ca9a7);
		FLOWS-INTO-CELL(ca7a9,ca7a10);
		FLOWS-INTO-CELL(ca9a7,ca10a7);
		FLOWS-INTO-CELL(ca7a10,ca7a11);
		FLOWS-INTO-CELL(ca10a7,ca11a7);
		FLOWS-INTO-CELL(ca7a11,ca7a12);
		FLOWS-INTO-CELL(ca11a7,ca12a7);
		FLOWS-INTO-CELL(ca7a12,ca7a13);
		FLOWS-INTO-CELL(ca12a7,ca13a7);
		FLOWS-INTO-CELL(ca7a13,ca7a15);
		FLOWS-INTO-CELL(ca13a7,ca15a7);
		FLOWS-INTO-CELL(ca7a15,ca7a16);
		FLOWS-INTO-CELL(ca15a7,ca16a7);
		FLOWS-INTO-CELL(ca7a16,ca7a17);
		FLOWS-INTO-CELL(ca16a7,ca17a7);
		FLOWS-INTO-CELL(ca7a17,ca7a18);
		FLOWS-INTO-CELL(ca17a7,ca18a7);
		FLOWS-INTO-CELL(ca7a18,ca7a19);
		FLOWS-INTO-CELL(ca18a7,ca19a7);
		FLOWS-INTO-CELL(ca7a19,ca7a20);
		FLOWS-INTO-CELL(ca19a7,ca20a7);
		FLOWS-INTO-CELL(ca14a1,ca14a2);
		FLOWS-INTO-CELL(ca1a14,ca2a14);
		FLOWS-INTO-CELL(ca14a2,ca14a3);
		FLOWS-INTO-CELL(ca2a14,ca3a14);
		FLOWS-INTO-CELL(ca14a3,ca14a4);
		FLOWS-INTO-CELL(ca3a14,ca4a14);
		FLOWS-INTO-CELL(ca14a4,ca14a5);
		FLOWS-INTO-CELL(ca4a14,ca5a14);
		FLOWS-INTO-CELL(ca14a5,ca14a6);
		FLOWS-INTO-CELL(ca5a14,ca6a14);
		FLOWS-INTO-CELL(ca14a6,ca14a8);
		FLOWS-INTO-CELL(ca6a14,ca8a14);
		FLOWS-INTO-CELL(ca14a8,ca14a9);
		FLOWS-INTO-CELL(ca8a14,ca9a14);
		FLOWS-INTO-CELL(ca14a9,ca14a10);
		FLOWS-INTO-CELL(ca9a14,ca10a14);
		FLOWS-INTO-CELL(ca14a10,ca14a11);
		FLOWS-INTO-CELL(ca10a14,ca11a14);
		FLOWS-INTO-CELL(ca14a11,ca14a12);
		FLOWS-INTO-CELL(ca11a14,ca12a14);
		FLOWS-INTO-CELL(ca14a12,ca14a13);
		FLOWS-INTO-CELL(ca12a14,ca13a14);
		FLOWS-INTO-CELL(ca14a13,ca14a15);
		FLOWS-INTO-CELL(ca13a14,ca15a14);
		FLOWS-INTO-CELL(ca14a15,ca14a16);
		FLOWS-INTO-CELL(ca15a14,ca16a14);
		FLOWS-INTO-CELL(ca14a16,ca14a17);
		FLOWS-INTO-CELL(ca16a14,ca17a14);
		FLOWS-INTO-CELL(ca14a17,ca14a18);
		FLOWS-INTO-CELL(ca17a14,ca18a14);
		FLOWS-INTO-CELL(ca14a18,ca14a19);
		FLOWS-INTO-CELL(ca18a14,ca19a14);
		FLOWS-INTO-CELL(ca14a19,ca14a20);
		FLOWS-INTO-CELL(ca19a14,ca20a14);
	};
	init-state {
		occupied(ca7a1);
		occupied(ca7a5);
		occupied(ca7a8);
		occupied(ca7a10);
		occupied(ca11a7);
		occupied(ca7a12);
		occupied(ca7a13);
		occupied(ca7a15);
		occupied(ca7a18);
		occupied(ca1a14);
		occupied(ca14a4);
		occupied(ca4a14);
		occupied(ca14a5);
		occupied(ca14a9);
		occupied(ca14a10);
		occupied(ca10a14);
		occupied(ca14a11);
		occupied(ca12a14);
		occupied(ca14a16);
		occupied(ca14a17);
		occupied(ca19a14);
		occupied(ca20a14);
	};
	horizon  = 40;
	discount = 1.0;
}


